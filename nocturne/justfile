_default:
    @just --list

# Install or update dependencies
setup:
    uv python install 3.14t
    uv sync --all-extras

# Clean build files
clean:
    rm -rf build/ dist/ *.egg-info/
    find . -name "*.pyc" -delete
    find . -name "__pycache__" -delete

# Format code & check code
check:
    #!/usr/bin/env bash
    uv run black .
    uv run ruff check . --preview
    uv run mypy py

    clang-format -i $(find cpp -name '*.cc') $(find cpp -name '*.hh')
    run-clang-tidy -p build/cmake -quiet -- -std=c++17

# Build with C++ tests
build:
    uv run python setup.py build_ext --with-tests
    uv pip install -e .

# Run unit tests only
test-unit:
    uv run python setup.py run_cpp_tests
    uv run pytest py/tests/test_config.py py/tests/test_watcher.py py/tests/test_www.py -v

# Run integration tests only
test-integration pattern="":
    uv run pytest -sv --timeout 32 py/tests/integration -v {{ if pattern != "" { "-k '" + pattern + "'" } else { "" } }}

# Run all tests
test:
    just test-unit
    just test-integration

# Create a distributable package
package: clean
    #!/usr/bin/env bash
    soir_version=$(grep '^version =' pyproject.toml | awk '// { print $3; }' | tr -d '"')
    python_version=$(grep '^requires-python = "==' pyproject.toml | awk '// { print $3; }' | tr -d '"' | tr -d '=')
    virtualenv=dist/.venv

    rm -rf dist
    mkdir -p dist/{etc,bin}

    uv python install ${python_version}
    uv run pip install --link-mode clone
    uv run python -m build --wheel
    py=$(uv python find ${python_version})
    $py -m venv --copies ${virtualenv}

    cp bin/soir dist/bin/soir

    tar -czf dist/soir-${soir_version}-{{arch()}}_{{os()}}.tar.gz -C dist bin etc .venv
