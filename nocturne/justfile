_default:
    @just --list

# Install or update dependencies
setup:
    uv sync --all-extras

# Clean build files
clean:
    rm -rf build/ dist/ *.egg-info/
    find . -name "*.pyc" -delete
    find . -name "__pycache__" -delete

# Format code & check code
check:
    uv run black .
    clang-format -i src/*.cc src/*.hh src/*/*.cc src/*/*.hh
    uv run ruff check .
    uv run mypy soir

# Run all checks
build:
    uv pip install -e . --verbose

# Run C++ tests
test-cpp:
    #!/usr/bin/env bash
    mkdir -p build/cmake
    cd build/cmake
    python_exe=$(uv run which python)
    cmake ../.. -DBUILD_TESTING=ON -DPython_EXECUTABLE="${python_exe}" -DPython_ROOT_DIR="$(dirname $(dirname ${python_exe}))"
    make
    ctest --verbose

# Run tests
test:
    just test-cpp
    uv run --no-project pytest

# Create a distributable package
package:
    #!/usr/bin/env bash
    version=$(grep '^version =' pyproject.toml | awk '// { print $3; }' | tr -d '"')
    virtualenv=dist/.venv

    rm -rf dist
    mkdir -p dist/{etc,bin}

    uv python install 3.13
    py=$(uv python find 3.13)
    $py -m venv --copies ${virtualenv}
    UV_PROJECT_ENVIRONMENT=${virtualenv} uv sync --no-editable

    cp bin/soir dist/bin/soir

    tar -czf dist/soir-${version}-{{arch()}}_{{os()}}.tar.gz -C dist bin etc .venv
