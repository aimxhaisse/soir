_default:
    @just --list

# Install or update dependencies
setup:
    uv python install 3.14
    uv sync --all-extras

# Clean build files
clean:
    rm -rf build/ dist/ *.egg-info/
    find . -name "*.pyc" -delete
    find . -name "__pycache__" -delete

# Format code & check code
check:
    uv run black .
    uv run ruff check .
    uv run mypy soir

    clang-format -i src/*.cc src/*.hh src/*/*.cc src/*/*.hh
    run-clang-tidy -p build/cmake -quiet -- -std=c++17

# Build with C++ tests
build:
    uv run python setup.py build_ext --with-tests

# Run all tests
test:
    uv run python setup.py run_cpp_tests
    uv run python setup.py run_python_tests

# Create a distributable package
package: clean
    #!/usr/bin/env bash
    soir_version=$(grep '^version =' pyproject.toml | awk '// { print $3; }' | tr -d '"')
    python_version=$(grep '^requires-python = "==' pyproject.toml | awk '// { print $3; }' | tr -d '"' | tr -d '=')
    virtualenv=dist/.venv

    rm -rf dist
    mkdir -p dist/{etc,bin}

    uv python install ${python_version}
    uv run python setup.py build_ext
    uv pip install -e . --verbose
    py=$(uv python find ${python_version})
    $py -m venv --copies ${virtualenv}
    UV_PROJECT_ENVIRONMENT=${virtualenv} uv sync --no-editable

    cp bin/soir dist/bin/soir

    tar -czf dist/soir-${soir_version}-{{arch()}}_{{os()}}.tar.gz -C dist bin etc .venv
